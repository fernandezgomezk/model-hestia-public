//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) Hestia 2021 - PBL & TNO                                        //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//           Dit is de container met rekenschema's die werken op Bebouwing              //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

//============== AcieveWoning pakketbepaling =====
unit<uint32> BouwdeelActieveWoning := select_with_org_rel(BO/Activatie/BouwdeelActief)
{
	attribute<BO>     BO_rel   := org_rel, ishidden = "True";
	attribute<rdc_meter> Geometry := lookup(BO_rel, BO/Geometry), ishidden = "True";
	
	container IsolatieNiveau_rel := for_each_nedv(classifications/bouwdeel/name, 'BO/Bouwdelen/Isolatie/'+classifications/bouwdeel/name+'[BO_rel]', ., Classifications/IsolatieNiveau), ishidden = "True";
	
	attribute<m2> TotalArea := ='add('+AsItemList('BO/Bouwdelen/'+Classifications/bouwdeel/name+'[BO_rel]')+')', ishidden = "True";

	unit<uint8> IV := classifications/IsolatieVerbetering, ishidden = "True";
		
	// ================ BEGIN Berekening t.b.v. minimum score
	// zie https://github.com/ObjectVision/SAVI2/issues/174

	attribute<GJ_yr> MaximumVraagWaarde := =voorkeuren/MinimumIsolatieNiveauEis ? 'voorkeuren/MinimumIsolatieNiveauEis/FunctioneleVraagLimiet_GJ_yr_m2 * BO/Oppervlakte[BO_rel]' : 'const(999999[GJ_Yr], BouwdeelActieveWoning)';
	
	container VerbruiksBijdrage := for_each_nedv(classifications/BouwdeelIsolatie/name
		, 	replace('BO/Functioneel_base/RV_N0_per_Bouwdeel_now_raw/@BD[BO_rel] * (1.0[ratio] - Classifications/BouwdeelIsolatie/R_RV[combine_data(Classifications/BouwdeelIsolatie, classifications/Bouwdeel/V/@BD, classifications/IsolatieNiveau/V/@IN)])'
			,	'@BD',    classifications/BouwdeelIsolatie/bouwdeel_name
			,	'@IN',    classifications/BouwdeelIsolatie/isolatieniveau_name)
		,	BouwdeelActieveWoning, GJ_Yr
	);
	container HuidigeVerbruiksBijdrage := for_each_nedv(classifications/Bouwdeel/name
		,	replace('merge(IsolatieNiveau_rel/@BD, GJ_Yr, '+AsItemList('VerbruiksBijdrage/@BD_' + classifications/IsolatieNiveau/name)+')'
			,	'@BD',    classifications/Bouwdeel/name
			)
		,	BouwdeelActieveWoning, GJ_Yr
	);
	
	attribute<GJ_yr> WorstKaasWaarde := ='add('+AsItemList('HuidigeVerbruiksBijdrage/'+classifications/Bouwdeel/name)+')';
	attribute<GJ_yr> BestKaasWaarde := ='add('+AsItemList('VerbruiksBijdrage/'+classifications/Bouwdeel/name+'_N4')+')';
	attribute<bool>  AltijdOKE := MaximumVraagWaarde > WorstKaasWaarde;
	attribute<bool>  NooitOKE  := MaximumVraagWaarde <= BestKaasWaarde;
	attribute<bool>  DynamicProgramForMinimumIsolatieNiveauEis := not(AltijdOKE) && not(NooitOKE);
	
	unit<uint8> VraagBucket := range(0b, 20b);
	
	
	unit<uint32> DP_Woning := select(DynamicProgramForMinimumIsolatieNiveauEis)
	{
		attribute<rdc_meter> Geometry := collect_by_cond(DP_Woning, BouwdeelActieveWoning/geometry);
		
		attribute<classifications/schillabel> DEBUG_Huidig_schillabel_rel := classify(collect_by_cond(DP_Woning, WorstKaasWaarde * 1000[MJ / GJ] / 3.6[MJ / kWh] / BO/Oppervlakte[BO_rel]), classifications/schillabel/ClassBreak);
		attribute<classifications/schillabel> DEBUG_Beste_schillabel_rel  := classify(collect_by_cond(DP_Woning, BestKaasWaarde  * 1000[MJ / GJ] / 3.6[MJ / kWh] / BO/Oppervlakte[BO_rel]), classifications/schillabel/ClassBreak);

		attribute<GJ_yr> VraagBucketSize := collect_by_cond(DP_Woning, MaximumVraagWaarde) / Float64(#VraagBucket);
		unit<uint32> DP_Bucket := combine(DP_Woning, VraagBucket)
		{
			container has := for_each_nedv(classifications/Bouwdeel/name,
				replace(
					AsList(replace('second_rel == (collect_by_cond(DP_Woning, VerbruiksBijdrage/@BD_@IN)[first_rel] / VraagBucketSize[first_rel])[VraagBucket]', '@IN', classifications/IsolatieDoel/name), '||')
				,	'@BD', classifications/Bouwdeel/name
				)
			,	DP_Bucket, bool);
			
			unit<uint32> DP_BucketIter := combine(DP_Bucket, VraagBucket)
			{
				attribute<DP_Bucket> DP_bucket_rel := combine_data(DP_Bucket, DP_Bucket/first_rel[first_rel], second_rel);
				attribute<VraagBucket> third_rel_raw := DP_Bucket/second_rel[first_rel] + second_rel;
				attribute<VraagBucket> third_rel := third_rel_raw < uint8(#VraagBucket) ? third_rel_raw : 0b/0b;
				attribute<DP_Bucket> DP_bucket_dest_rel := combine_data(DP_Bucket, DP_Bucket/first_rel[first_rel], third_rel);
			}
			
			// max 16
			attribute<uint8> CountWays1 := sum_uint8(has/RB[DP_BucketIter/first_rel] && has/RO[DP_BucketIter/DP_bucket_rel], DP_BucketIter/DP_bucket_dest_rel); 
			attribute<uint8> CountWays2 := sum_uint8(has/DR[DP_BucketIter/first_rel] && has/PL[DP_BucketIter/DP_bucket_rel], DP_BucketIter/DP_bucket_dest_rel);
			attribute<uint8> CountWays3 := sum_uint8(has/VL[DP_BucketIter/first_rel] && has/MG[DP_BucketIter/DP_bucket_rel], DP_BucketIter/DP_bucket_dest_rel);
			attribute<uint8> CountWays4 := sum_uint8(has/MS[DP_BucketIter/first_rel] && has/DP[DP_BucketIter/DP_bucket_rel], DP_BucketIter/DP_bucket_dest_rel);
			attribute<uint8> CountWays5 := sum_uint8(has/DS[DP_BucketIter/first_rel] && has/KR[DP_BucketIter/DP_bucket_rel], DP_BucketIter/DP_bucket_dest_rel);
			
			attribute<uint16> CountWaysA := sum_uint16(CountWays1[DP_BucketIter/first_rel] * CountWays2[DP_BucketIter/DP_bucket_rel], DP_BucketIter/DP_bucket_dest_rel); // max 16^2 == 625
			attribute<uint16> CountWaysB := sum_uint16(CountWays3[DP_BucketIter/first_rel] * CountWays4[DP_BucketIter/DP_bucket_rel], DP_BucketIter/DP_bucket_dest_rel); // max 16^2 == 625
			attribute<uint32> CountWaysC := sum_uint32(CountWaysA[DP_BucketIter/first_rel] * CountWaysB[DP_BucketIter/DP_bucket_rel], DP_BucketIter/DP_bucket_dest_rel); // max 256^2 == 390625
			attribute<uint32> CountWaysX := sum_uint32(CountWaysC[DP_BucketIter/first_rel] * uint32(CountWays5[DP_BucketIter/DP_bucket_rel]), DP_BucketIter/DP_bucket_dest_rel); // max 4^10 == 1024^2
			attribute<uint32> CumulCountWaysX := cumulate(CountWaysX, first_rel); 
			
			attribute<uint32> CountTest(DP_Woning) := sum(CountWaysX, first_rel);
			
		}
	}
	
	// ================ END   Berekening t.b.v. minimum score


	unit<uint32> AmbitieBerekening := combine(BouwdeelActieveWoning, IV)
	,	Descr = "iedere (relevante) isolatie ambitie voor iedere op bouwdeel geactiveerde woning"
	{
		attribute<BouwdeelActieveWoning> BouwdeelActieveWoning_rel := first_rel, IsHidden = "True"; // first_rel
		attribute<IV> IsolatieVerbetering_rel := second_rel, IsHidden = "True"; // second_rel
		
		attribute<BO> BO_rel  := BouwdeelActieveWoning/BO_rel[BouwdeelActieveWoning_rel], ishidden = "True";

		attribute<classifications/IsolatieAmbitie> IsolatieAmbitie_rel := IV/IsolatieAmbitie_rel[IsolatieVerbetering_rel];
		
		attribute<float64> beta  (Classifications/IsolatieVerbetering) := IV/IsolatieAmbitie_rel->BesparingsFactor;
		
		parameter<GJ_Yr>   VerbruiksIndicatie := mean(BO/Functioneel/RV)                             , descr = "indicatie verbruik per woning in GJ_Jr";
		parameter<EUR_GJ>  EnergiePrijs       := first(prijzen/Aardgas/Staffel/KGJ_eindgebruik_excl) , descr = "kosten per GJ";
		parameter<Yr>      Duration           := mean(Classifications/Bouwdeel/AT[yr])       , descr = "afschrijftijd voor rentabiliteitsafweging";
		parameter<EUR>     CombiFactor        := (VerbruiksIndicatie * EnergiePrijs * Duration);
		
		// hoeveel besparing in verlies
		
		// xx_N1 behoeft geen afweging, geen besparing noch kosten.
		attribute<Float64> BesparingsFactor(classifications/IsolatieMaatregel) := 
			classifications/Bouwdeel/Verlies[classifications/IsolatieMaatregel/bouwdeel_rel] // hoeveel relatief belang 
		*	MakeDefined(classifications/IsolatieMaatregel/R_RV, 0.0);
		
		unit<uint32> IMxUV := classifications/IsolatieMaatregel/xUitvoering, ishidden = "True";
		
		container SpecificP50P := for_each_nedv(classifications/IsolatieMaatregel/name
		,	replace('Classifications/Scurve_isolatie/p50p_f[@EDxIM]'
			,	'@EDxIM', 'combine_data(Classifications/Scurve_isolatie, BO/Eigendom_rel[BO_rel], @IM)'
			,	'@IM',    'classifications/IsolatieMaatregel/V/'+classifications/IsolatieMaatregel/name
			)
		,	AmbitieBerekening, float64
		);
		
		// IsolatieScore: xx_N1 behoeft geen afweging, geen besparing en ook 0 kosten; 
		// huidig niveau's (indien > N1) behoeven wel afweging teneinde tegenwicht te bieden voor besparingen bij volgended IsolatieNiveau's
		container IsolatieScore := for_each_nedv(classifications/IsolatieMaatregel/name
		,	replace(
				'iif(IsolatieNiveau_rel/@BD[BouwdeelActieveWoning_rel] <=  @IN'
				',	BO/Bouwdelen/@BD[BO_rel]'
					'*	( ( Combifactor * BesparingsFactor[@IM] * beta[IsolatieVerbetering_rel]  )'
					'/	TotalArea[BouwdeelActieveWoning_rel] - iif(IsolatieNiveau_rel/@BD[BouwdeelActieveWoning_rel] < @IN, IMxUV/kosten[@IMxUV] * @LC * @BTW * SpecificP50P/@IBD, 0[EUR_m2]))'
				',	(0.0 / 0.0)'
				')'
			,	'@BD',    classifications/IsolatieMaatregel/bouwdeel_name
			,	'@IN',    'classifications/IsolatieNiveau/V/'+classifications/IsolatieMaatregel/IsolatieNiveau_name
			,	'@IBD',   classifications/IsolatieMaatregel/name
			,	'@IMxUV', 'combine_data(IMxUV, @IM, BO/Activatie/Uitvoering[BO_rel])'
			,	'@IM',    'classifications/IsolatieMaatregel/V/'+classifications/IsolatieMaatregel/name
			,	'@BTW',   '(1.0[ratio] + Invoer/Kengetallen/BTW/BTW_gv[Zichtjaar_rel] / 100[percent])'
			,	'@LC',    'leerCurves/Isolatie/Curve'
			)
		, 	AmbitieBerekening, float64
		);
		container SpecificBeta := for_each_nedv(classifications/IsolatieMaatregel/name
		,	replace('Classifications/Scurve_isolatie/beta_f[@EDxIM]'
			,	'@EDxIM', 'combine_data(Classifications/Scurve_isolatie, BO/Eigendom_rel[BO_rel], @IM)'
			,	'@IM',    'classifications/IsolatieMaatregel/V/'+classifications/IsolatieMaatregel/name
			)
		,	AmbitieBerekening, float64
		);

		container Geschiktheid := for_each_nedv(classifications/IsolatieMaatregel/name
			, replace('IsolatieScore/@BDI - Min_elem('+ AsItemList('IsolatieScore/@BDN_'+/Classifications/IsolatieDoel/name) +')' 
				,	'@BDI', classifications/IsolatieMaatregel/name
				,	'@BDN', classifications/IsolatieMaatregel/bouwdeel_name
				)
			,AmbitieBerekening
			,float64);

		// TODO: check for overflow
//		parameter<float64> LOG_MAX_FLOAT64 := 700.0, ishidden = "True"; // slightly less than ln( 2^1024 / pcount(classifications/BouwdeelIsolatie/bouwdeel_rel) )
		parameter<float64> LOG_MAX_FLOAT64 := 600.0, ishidden = "True"; // avoid overflow when multiplying these
		container OddsRatio := for_each_nedv(classifications/IsolatieMaatregel/name
		,	replace('IsDefined(Geschiktheid/@BDI) ? exp(min_elem(Geschiktheid/@BDI * SpecificBeta/@BDI / 1[EUR], LOG_MAX_FLOAT64)) : 0.0'
			,	'@BDI', classifications/IsolatieMaatregel/name)
		,	AmbitieBerekening, float64
		);
		
		// ================ BEGIN Berekening t.b.v. minimum score Ambitie specifiek deel
		// zie https://github.com/ObjectVision/SAVI2/issues/174
		
		attribute<bool> AmbitieHeeftRestrictie := DynamicProgramForMinimumIsolatieNiveauEis[BouwdeelActieveWoning_rel];
		unit<uint32> DP_WoningAmbitie := select(AmbitieHeeftRestrictie)
		{
			attribute<GJ_Yr> VraagBucketSize := collect_by_cond(DP_WoningAmbitie, MaximumVraagWaarde[BouwdeelActieveWoning_rel]) / Float64(#VraagBucket + #Classifications/Bouwdeel);
			container OddsRatio_raw := for_each_nedv(classifications/IsolatieMaatregel/name,
				'collect_by_cond(DP_WoningAmbitie, .../OddsRatio/'+classifications/IsolatieMaatregel/name+')'
			,	DP_WoningAmbitie, float64
			)
			{
				container Total := for_each_nedv(classifications/bouwdeel/name
				, 'add('+AsItemList(classifications/IsolatieMaatregel/name, classifications/IsolatieMaatregel/bouwdeel_rel)+')'
				, DP_WoningAmbitie, float64
				);
			}
			
			container OddsRatio := for_each_nedv(classifications/IsolatieMaatregel/name
			, 'OddsRatio_raw/'+classifications/IsolatieMaatregel/name + ' / OddsRatio_raw/Total/'+classifications/IsolatieMaatregel/bouwdeel_name
			, DP_WoningAmbitie, float64
			);
			
			container VerbruiksBijdrage := for_each_nedv(classifications/IsolatieMaatregel/name,
				'collect_by_cond(DP_WoningAmbitie, ..../VerbruiksBijdrage/'+classifications/IsolatieMaatregel/name+'[BouwdeelActieveWoning_rel])'
			,	DP_WoningAmbitie, float64
			);
			
			container VerbruiksBucket := for_each_nedv(classifications/IsolatieMaatregel/name
			,	'(VerbruiksBijdrage/'+classifications/IsolatieMaatregel/name+' / VraagBucketSize)[VraagBucket]'
			,	DP_WoningAmbitie, VraagBucket
			);
			
			unit<uint32> DP_Bucket := combine(DP_WoningAmbitie, VraagBucket)
			{
				container BucketHit := for_each_nedv(classifications/IsolatieMaatregel/name
				,	'second_rel == VerbruiksBucket/'+classifications/IsolatieMaatregel/name+'[first_rel]'
				,	DP_Bucket, bool
				);
				
				container BucketMeasure := for_each_nedv(classifications/Bouwdeel/name,
					replace(
						AsList(
							replace('OddsRatio/@BD_@IN[first_rel] * float64(BucketHit/@BD_@IN)'
							, '@IN', classifications/IsolatieDoel/name)
						, '+'
						)
					,	'@BD', classifications/Bouwdeel/name
					)
				,	DP_Bucket, float64);
				
				unit<uint32> BucketIter := combine(DP_Bucket, VraagBucket)
				{
					attribute<DP_Bucket> DP_bucket_rel := combine_data(DP_Bucket, DP_Bucket/first_rel[first_rel], second_rel);
					attribute<VraagBucket> third_rel_raw := DP_Bucket/second_rel[first_rel] + second_rel;
					attribute<VraagBucket> third_rel := third_rel_raw < uint8(#VraagBucket) ? third_rel_raw : 0b/0b;
					attribute<DP_Bucket> DP_bucket_dest_rel := combine_data(DP_Bucket, DP_Bucket/first_rel[first_rel], third_rel);
				}
			
//				unit<uint32> BucketIter := DP_Woning/DP_Bucket/DP_BucketIter;
//				attribute<float64> MeasureWays0 := const(1.0, DP_Bucket);
				attribute<float64> MeasureWays1 := BucketMeasure/RB; // sum(MeasureWays0[BucketIter/first_rel] * BucketMeasure/RB[BucketIter/DP_bucket_rel], BucketIter/DP_bucket_dest_rel);
				attribute<float64> MeasureWays2 := sum(MeasureWays1[BucketIter/first_rel] * BucketMeasure/RO[BucketIter/DP_bucket_rel], BucketIter/DP_bucket_dest_rel);
				attribute<float64> MeasureWays3 := sum(MeasureWays2[BucketIter/first_rel] * BucketMeasure/DR[BucketIter/DP_bucket_rel], BucketIter/DP_bucket_dest_rel);
				attribute<float64> MeasureWays4 := sum(MeasureWays3[BucketIter/first_rel] * BucketMeasure/PL[BucketIter/DP_bucket_rel], BucketIter/DP_bucket_dest_rel);
				attribute<float64> MeasureWays5 := sum(MeasureWays4[BucketIter/first_rel] * BucketMeasure/VL[BucketIter/DP_bucket_rel], BucketIter/DP_bucket_dest_rel);
				attribute<float64> MeasureWays6 := sum(MeasureWays5[BucketIter/first_rel] * BucketMeasure/MG[BucketIter/DP_bucket_rel], BucketIter/DP_bucket_dest_rel);
				attribute<float64> MeasureWays7 := sum(MeasureWays6[BucketIter/first_rel] * BucketMeasure/MS[BucketIter/DP_bucket_rel], BucketIter/DP_bucket_dest_rel);
				attribute<float64> MeasureWays8 := sum(MeasureWays7[BucketIter/first_rel] * BucketMeasure/DP[BucketIter/DP_bucket_rel], BucketIter/DP_bucket_dest_rel);
				attribute<float64> MeasureWays9 := sum(MeasureWays8[BucketIter/first_rel] * BucketMeasure/DS[BucketIter/DP_bucket_rel], BucketIter/DP_bucket_dest_rel);				
				attribute<float64> MeasureWaysX := sum(MeasureWays9[BucketIter/first_rel] * BucketMeasure/KR[BucketIter/DP_bucket_rel], BucketIter/DP_bucket_dest_rel);
				
				attribute<float64> RescaledWaysX := MeasureWaysX / sum(MeasureWaysX, first_rel)[first_rel];
				attribute<float64> CumulWaysX := cumulate(RescaledWaysX, first_rel);
				
				attribute<float64> RemeasureWays9 := MeasureWays9 * BucketMeasure/KR[combine_data(DP_Bucket, first_rel, sub_or_null(chosenX[first_rel], second_rel))];
				attribute<float64> RescaledWays9 := RemeasureWays9 / sum(RemeasureWays9, first_rel)[first_rel];
				attribute<float64> CumulWays9 := cumulate(RescaledWays9, first_rel);
				
				attribute<float64> RemeasureWays8 := MeasureWays8 * BucketMeasure/DS[combine_data(DP_Bucket, first_rel, sub_or_null(chosen9[first_rel], second_rel))];
				attribute<float64> RescaledWays8 := RemeasureWays8 / sum(RemeasureWays8, first_rel)[first_rel];
				attribute<float64> CumulWays8 := cumulate(RescaledWays8, first_rel);
				
				attribute<float64> RemeasureWays7 := MeasureWays7 * BucketMeasure/DP[combine_data(DP_Bucket, first_rel, sub_or_null(chosen8[first_rel], second_rel))];
				attribute<float64> RescaledWays7 := RemeasureWays7 / sum(RemeasureWays7, first_rel)[first_rel];
				attribute<float64> CumulWays7 := cumulate(RescaledWays7, first_rel);
				
				attribute<float64> RemeasureWays6 := MeasureWays6 * BucketMeasure/MS[combine_data(DP_Bucket, first_rel, sub_or_null(chosen7[first_rel], second_rel))];
				attribute<float64> RescaledWays6 := RemeasureWays6 / sum(RemeasureWays6, first_rel)[first_rel];
				attribute<float64> CumulWays6 := cumulate(RescaledWays6, first_rel);
				
				attribute<float64> RemeasureWays5 := MeasureWays5 * BucketMeasure/MG[combine_data(DP_Bucket, first_rel, sub_or_null(chosen6[first_rel], second_rel))];
				attribute<float64> RescaledWays5 := RemeasureWays5 / sum(RemeasureWays5, first_rel)[first_rel];
				attribute<float64> CumulWays5 := cumulate(RescaledWays5, first_rel);
				
				attribute<float64> RemeasureWays4 := MeasureWays4 * BucketMeasure/VL[combine_data(DP_Bucket, first_rel, sub_or_null(chosen5[first_rel], second_rel))];
				attribute<float64> RescaledWays4 := RemeasureWays4 / sum(RemeasureWays4, first_rel)[first_rel];
				attribute<float64> CumulWays4 := cumulate(RescaledWays4, first_rel);
				
				attribute<float64> RemeasureWays3 := MeasureWays3 * BucketMeasure/PL[combine_data(DP_Bucket, first_rel, sub_or_null(chosen4[first_rel], second_rel))];
				attribute<float64> RescaledWays3 := RemeasureWays3 / sum(RemeasureWays3, first_rel)[first_rel];
				attribute<float64> CumulWays3 := cumulate(RescaledWays3, first_rel);
				
				attribute<float64> RemeasureWays2 := MeasureWays2 * BucketMeasure/DR[combine_data(DP_Bucket, first_rel, sub_or_null(chosen3[first_rel], second_rel))];
				attribute<float64> RescaledWays2 := RemeasureWays2 / sum(RemeasureWays2, first_rel)[first_rel];
				attribute<float64> CumulWays2 := cumulate(RescaledWays2, first_rel);
				
				attribute<float64> RemeasureWays1 := MeasureWays1 * BucketMeasure/RO[combine_data(DP_Bucket, first_rel, sub_or_null(chosen2[first_rel], second_rel))];
				attribute<float64> RescaledWays1 := RemeasureWays1 / sum(RemeasureWays1, first_rel)[first_rel];
				attribute<float64> CumulWays1 := cumulate(RescaledWays1, first_rel);
				
				attribute<float64> RemeasureWays0 := BucketMeasure/RB[combine_data(DP_Bucket, first_rel, sub_or_null(chosen1[first_rel], second_rel))];
				attribute<float64> RescaledWays0 := RemeasureWays0 / sum(RemeasureWays0, first_rel)[first_rel];
				attribute<float64> CumulWays0 := cumulate(RescaledWays0, first_rel);
			}
			
			attribute<float64> rndX := rnd_uniform(10, DP_WoningAmbitie, range(0.0, 1.0));
			attribute<float64> rnd9 := rnd_uniform( 9, DP_WoningAmbitie, range(0.0, 1.0));
			attribute<float64> rnd8 := rnd_uniform( 8, DP_WoningAmbitie, range(0.0, 1.0));
			attribute<float64> rnd7 := rnd_uniform( 7, DP_WoningAmbitie, range(0.0, 1.0));
			attribute<float64> rnd6 := rnd_uniform( 6, DP_WoningAmbitie, range(0.0, 1.0));
			attribute<float64> rnd5 := rnd_uniform( 5, DP_WoningAmbitie, range(0.0, 1.0));
			attribute<float64> rnd4 := rnd_uniform( 4, DP_WoningAmbitie, range(0.0, 1.0));
			attribute<float64> rnd3 := rnd_uniform( 3, DP_WoningAmbitie, range(0.0, 1.0));
			attribute<float64> rnd2 := rnd_uniform( 2, DP_WoningAmbitie, range(0.0, 1.0));
			attribute<float64> rnd1 := rnd_uniform( 1, DP_WoningAmbitie, range(0.0, 1.0));
			attribute<float64> rnd0 := rnd_uniform( 0, DP_WoningAmbitie, range(0.0, 1.0));
			
			attribute<VraagBucket> chosenX := =AsList(replace('rndX <= DP_Bucket/CumulWaysX[combine_data(DP_Bucket, ID(DP_WoningAmbitie), @VB[VraagBucket])] ? @VB[VraagBucket] :', '@VB', string(ID(VraagBucket))), '') + '(0/0)[VraagBucket]';
			attribute<VraagBucket> chosen9 := =AsList(replace('rnd9 <= DP_Bucket/CumulWays9[combine_data(DP_Bucket, ID(DP_WoningAmbitie), @VB[VraagBucket])] ? @VB[VraagBucket] :', '@VB', string(ID(VraagBucket))), '') + '(0/0)[VraagBucket]';
			attribute<VraagBucket> chosen8 := =AsList(replace('rnd8 <= DP_Bucket/CumulWays8[combine_data(DP_Bucket, ID(DP_WoningAmbitie), @VB[VraagBucket])] ? @VB[VraagBucket] :', '@VB', string(ID(VraagBucket))), '') + '(0/0)[VraagBucket]';
			attribute<VraagBucket> chosen7 := =AsList(replace('rnd7 <= DP_Bucket/CumulWays7[combine_data(DP_Bucket, ID(DP_WoningAmbitie), @VB[VraagBucket])] ? @VB[VraagBucket] :', '@VB', string(ID(VraagBucket))), '') + '(0/0)[VraagBucket]';
			attribute<VraagBucket> chosen6 := =AsList(replace('rnd6 <= DP_Bucket/CumulWays6[combine_data(DP_Bucket, ID(DP_WoningAmbitie), @VB[VraagBucket])] ? @VB[VraagBucket] :', '@VB', string(ID(VraagBucket))), '') + '(0/0)[VraagBucket]';
			attribute<VraagBucket> chosen5 := =AsList(replace('rnd5 <= DP_Bucket/CumulWays5[combine_data(DP_Bucket, ID(DP_WoningAmbitie), @VB[VraagBucket])] ? @VB[VraagBucket] :', '@VB', string(ID(VraagBucket))), '') + '(0/0)[VraagBucket]';
			attribute<VraagBucket> chosen4 := =AsList(replace('rnd4 <= DP_Bucket/CumulWays4[combine_data(DP_Bucket, ID(DP_WoningAmbitie), @VB[VraagBucket])] ? @VB[VraagBucket] :', '@VB', string(ID(VraagBucket))), '') + '(0/0)[VraagBucket]';
			attribute<VraagBucket> chosen3 := =AsList(replace('rnd3 <= DP_Bucket/CumulWays3[combine_data(DP_Bucket, ID(DP_WoningAmbitie), @VB[VraagBucket])] ? @VB[VraagBucket] :', '@VB', string(ID(VraagBucket))), '') + '(0/0)[VraagBucket]';
			attribute<VraagBucket> chosen2 := =AsList(replace('rnd2 <= DP_Bucket/CumulWays2[combine_data(DP_Bucket, ID(DP_WoningAmbitie), @VB[VraagBucket])] ? @VB[VraagBucket] :', '@VB', string(ID(VraagBucket))), '') + '(0/0)[VraagBucket]';
			attribute<VraagBucket> chosen1 := =AsList(replace('rnd1 <= DP_Bucket/CumulWays1[combine_data(DP_Bucket, ID(DP_WoningAmbitie), @VB[VraagBucket])] ? @VB[VraagBucket] :', '@VB', string(ID(VraagBucket))), '') + '(0/0)[VraagBucket]';
			attribute<VraagBucket> chosen0 := =AsList(replace('rnd0 <= DP_Bucket/CumulWays0[combine_data(DP_Bucket, ID(DP_WoningAmbitie), @VB[VraagBucket])] ? @VB[VraagBucket] :', '@VB', string(ID(VraagBucket))), '') + '(0/0)[VraagBucket]';
				
			attribute<VraagBucket> chosenKR(DP_WoningAmbitie) := chosenX - chosen9;
			attribute<VraagBucket> chosenDS(DP_WoningAmbitie) := chosen9 - chosen8;
			attribute<VraagBucket> chosenDP(DP_WoningAmbitie) := chosen8 - chosen7;
			attribute<VraagBucket> chosenMS(DP_WoningAmbitie) := chosen7 - chosen6;
			attribute<VraagBucket> chosenMG(DP_WoningAmbitie) := chosen6 - chosen5;
			attribute<VraagBucket> chosenVL(DP_WoningAmbitie) := chosen5 - chosen4;
			attribute<VraagBucket> chosenPL(DP_WoningAmbitie) := chosen4 - chosen3;
			attribute<VraagBucket> chosenDR(DP_WoningAmbitie) := chosen3 - chosen2;
			attribute<VraagBucket> chosenRO(DP_WoningAmbitie) := chosen2 - chosen1;
			attribute<VraagBucket> chosenRB(DP_WoningAmbitie) := chosen1 - chosen0;
				
			container IM_on_chosenPath := for_each_nedv(classifications/IsolatieMaatregel/name
			, 	replace('chosen@BD == VerbruiksBucket/@IM'
				,	'@IM', classifications/IsolatieMaatregel/name
				,	'@BD', classifications/IsolatieMaatregel/Bouwdeel_name
				)
			,	DP_WoningAmbitie, bool
			)
			{
				container DEBUG_count := for_each_nedv(classifications/bouwdeel/name
				, 'add('+AsItemList('uint8('+classifications/IsolatieMaatregel/name+')', classifications/IsolatieMaatregel/bouwdeel_rel)+')'
				, DP_WoningAmbitie, UInt8
				);
			}
			attribute<classifications/schillabel> DEBUG_RV_raw_schillabel := collect_by_cond(DP_WoningAmbitie, UniekAmbitie/Functioneel/RV_raw_Schillabel_rel[UniekAmbitie_rel]);
		}
		// ================ END   Berekening t.b.v. minimum score Ambitie specifiek deel
			
		container IM_on_chosenPath := for_each_nedv(classifications/IsolatieMaatregel/name
		, 	'recollect_by_cond(AmbitieHeeftRestrictie, DP_WoningAmbitie/IM_on_chosenPath/'+classifications/IsolatieMaatregel/name+', true)'
		,	AmbitieBerekening, bool
		);
		
		container RestrictedOddsRatio := for_each_nedv(classifications/IsolatieMaatregel/name
		, 	replace('OddsRatio/@IM * float64(IM_on_chosenPath/@IM)'
			,	'@IM', classifications/IsolatieMaatregel/name
			)
		,	AmbitieBerekening, float64
		)
		{
			container Total := for_each_nedv(classifications/bouwdeel/name
			, 'add(float64(not(AmbitieHeeftRestrictie)), '+AsItemList(classifications/IsolatieMaatregel/name, classifications/IsolatieMaatregel/bouwdeel_rel)+')'
			, AmbitieBerekening, float64
			);
		}
		
		container P := for_each_nedv(classifications/IsolatieMaatregel/name
		, 'RestrictedOddsRatio/'+classifications/IsolatieMaatregel/name + ' / RestrictedOddsRatio/Total/'+classifications/IsolatieMaatregel/bouwdeel_name
		, AmbitieBerekening, float64
		);
		
		
		container KiesBouwdelen := for_each_ne(classifications/Bouwdeel/name
		,	replace('KiesBouwDeelT(classifications/Bouwdeel/V/@BD, IsolatieNiveau_rel/@BD)'
			,	'@BD', classifications/Bouwdeel/name
			)
		);
		
		attribute<uint16> IsolatieCode1 := 
			= 'add('+AsItemList(
				classifications/Bouwdeel/IsolatieCodeSubFactor1!=0w 
				?	replace('uint16(AmbitieBerekening/KiesBouwdelen/@BD/NewIsolatieNiveau_rel) * classifications/Bouwdeel/V/@BD->IsolatieCodeSubFactor1', '@BD', classifications/Bouwdeel/name)
				:	string(0/0)
				)+')', ishidden = "True";
		attribute<uint16> IsolatieCode2 := 
			= 'add('+AsItemList(
				classifications/Bouwdeel/IsolatieCodeSubFactor2!=0w 
				?	replace('uint16(AmbitieBerekening/KiesBouwdelen/@BD/NewIsolatieNiveau_rel) * classifications/Bouwdeel/V/@BD->IsolatieCodeSubFactor2', '@BD', classifications/Bouwdeel/name)
				:	string(0/0)
				)+')', ishidden = "True";
		attribute<uint32> IsolatieCode := UInt32(IsolatieCode1)+3125*UInt32(IsolatieCode2), ishidden = "True";

		attribute<UniekIsolatieCode> UniekIsolatieCode_rel := rlookup(IsolatieCode, UniekIsolatieCode/values), ishidden = "True";
		attribute<UICxBAW> UICxBAW_code := point(BouwdeelActieveWoning_rel, UniekIsolatieCode_rel, UICxBAW), ishidden = "True";
			
		attribute<UniekAmbitie> UniekAmbitie_rel := rlookup(UICxBAW_code, UniekAmbitie/values), ishidden = "True";
	}
	unit<uint32> UniekIsolatieCode:= unique(AmbitieBerekening/IsolatieCode), ishidden = "True";
	unit<UPoint> UICxBAW : ishidden = "True";
	
	unit<uint32> UniekAmbitie := unique(AmbitieBerekening/UICxBAW_code)
	{
		attribute<BouwdeelActieveWoning>           BouwdeelActieveWoning_rel := value(PointRow(values), BouwdeelActieveWoning), ishidden = "True";
		attribute<BO>                              BO_Rel                    := BouwdeelActieveWoning_rel->BO_rel, ishidden = "True";
		attribute<UniekIsolatieCode>               UniekIsolatieCode_rel     := value(PointCol(values), UniekIsolatieCode), ishidden = "True";
		attribute<uint32>                          IsolatieCode              := UniekIsolatieCode_rel->values, ishidden = "True";
		attribute<classifications/IsolatieAmbitie> MinIsolatieAmbitie_rel    := min(AmbitieBerekening/IsolatieAmbitie_rel, AmbitieBerekening/UniekAmbitie_rel), ishidden = "True";  // take the lowest for P50P lookup
		attribute<classifications/IsolatieAmbitie> MaxIsolatieAmbitie_rel    := max(AmbitieBerekening/IsolatieAmbitie_rel, AmbitieBerekening/UniekAmbitie_rel), ishidden = "True";  // take the lowest for P50P lookup
		attribute<bool>                            DontApplyChange           := MinIsolatieAmbitie_rel == classifications/IsolatieAmbitie/V/geen && not(DynamicProgramForMinimumIsolatieNiveauEis[BouwdeelActieveWoning_rel]);
		attribute<m2>                              Oppervlakte_i             := BO/Oppervlakte[BO_Rel], ishidden = "True";
		attribute<nrAsl>                           nrAansluitingen_i         := BO/nrAansluitingen[BO_Rel], ishidden = "True";
		attribute<Classifications/Woningtype>      WoningType_rel            := BO/WoningType_rel[BO_Rel], ishidden = "True";

		
		container Bouwdelen := for_each_nedv(classifications/Bouwdeel/name
		,	replace('BO/Bouwdelen/@BD[BO_rel]',	'@BD', classifications/Bouwdeel/name)
		,	UniekAmbitie, m2)
		{
			container Isolatie_i := for_each_nedv(classifications/Bouwdeel/name
			,	replace('value((IsolatieCode / classifications/Bouwdeel/V/@BD->IsolatieCodeFactor) % 5, Classifications/IsolatieNiveau)', '@BD', classifications/Bouwdeel/name) //TODO check: %4 -> %5 bij aanpassing aantal isolatieviveaus?
			,	UniekAmbitie, Classifications/IsolatieNiveau);
			
			container IsChangedNow_i := for_each_nedv(
				classifications/Bouwdeel/name,
				replace('Isolatie_i/@BD > BO/Bouwdelen/Isolatie/@BD[BO_rel]'
				,	'@BD', classifications/Bouwdeel/name),
				UniekAmbitie, bool);

			container Normering
			{
				attribute<m2>        TotaalSchil (BO) := ='add('+AsItemList('BO/Bouwdelen/'+classifications/bouwdeel/name)+') - BO/Bouwdelen/KR';
				attribute<ratio>     Schilfactor (BO) := Totaalschil / BO/oppervlakte;

				parameter<bool> GeenNormen := sum(BBN/naleving) <= 0.0[ratio];

				attribute<ratio> AandeelRenovatie (UniekAmbitie) := ='(add('+AsItemList('Bouwdelen/'+classifications/bouwdeel/name+' * float64(IsChangedNow_i/'+classifications/bouwdeel/name+')')+') - (float64(IsChangedNow_i/KR) * Bouwdelen/KR) ) / TotaalSchil[BO_rel]';
				unit<uint32> BBN := ZichtjaarBeleid/BestaandeBouwNorm, ishidden = "True";
				container Normen := for_each_ne(BBN/name, 'ImplSubs(BBN/V/'+BBN/name+')');
				template ImplSubs : ishidden = "True"
				{
					//case parameters
					parameter<BBN> BBN_rel;
					//end case parameters

					attribute<bool>  Naleving (BO) := rnd_uniform(JaarSeed * 106u, BO, range(float64, 0d, 1d)) <= BBN/Naleving[BBN_rel];
					

					container voorwaarden
					{
						parameter<ratio>     MinSchilAandeel := BBN/voorwaarde/MinSchilAandeel[BBN_rel];
						parameter<ratio>     MinSchilfactor  := BBN/voorwaarde/MinSchilfactor[BBN_rel];
						parameter<ratio>     MaxSchilfactor  := BBN/voorwaarde/MaxSchilfactor[BBN_rel];
						parameter<yr_uint16> MinBouwjaar     := BBN/voorwaarde/MinBouwjaar[BBN_rel];
						parameter<yr_uint16> MaxBouwjaar     := BBN/voorwaarde/MaxBouwjaar[BBN_rel];

						attribute<bool> InDoelgroepED (BO) := ='OR('+AsItemList(replace('BBN/doelgroep/@ED[BBN_rel] && BO/Eigendom_rel == Classifications/Eigendom/V/@ED','@ED',Classifications/Eigendom/label))+')', ishidden = "True";
						attribute<bool> InDoelgroepWT (BO) := BBN/doelgroep/EGW[BBN_rel] && BO/IsMeergezins || BBN/doelgroep/MGW[BBN_rel] && not(BO/IsMeergezins);

						attribute<bool> SchilAandeelVoldaan (UniekAmbitie) := MakeDefined(AandeelRenovatie, -1.0) >= MakeDefined(MinSchilAandeel, 0.0);
						attribute<bool> SchilfactorVoldaan  (BO) := MakeDefined(Schilfactor, 0.0) >= MakeDefined(MinSchilfactor, 0.0) && MakeDefined(Schilfactor, 0.0) < MakeDefined(MaxSchilfactor, 9999.0);
						attribute<bool> BouwjaarVoldaan     (BO) := MakeDefined(BO/bouwjaar,  0w) >= MakeDefined(MinBouwjaar, 0w)     && MakeDefined(BO/bouwjaar,  0w) < MakeDefined(MaxBouwjaar, 9999w);

						attribute<bool> VoorwaardenVoldaan  (UniekAmbitie) := AND(SchilAandeelVoldaan,SchilfactorVoldaan[BO_rel],BouwjaarVoldaan[BO_rel],InDoelgroepED[BO_rel],InDoelgroepWT[BO_rel]);
					}

					attribute<bool>  Geldig   (UniekAmbitie) := voorwaarden/VoorwaardenVoldaan && Naleving[BO_rel];
				}
			}

			container ApplyIsolatie := for_each_nedv(classifications/Bouwdeel/name
			,	replace(' DontApplyChange ? BO/Bouwdelen/Isolatie/@BD@[BO_rel] : Isolatie_i/@BD@', '@BD@', Classifications/Bouwdeel/name) 
			,	UniekAmbitie, classifications/IsolatieNiveau
			);
			
			container Isolatie := for_each_nedv(classifications/Bouwdeel/name
			,	normering/GeenNormen 
				?  replace(' ApplyIsolatie/@BD@', '@BD@', Classifications/Bouwdeel/name) 
				:  replace(' max_elem('+AsItemList(replace('Normering/Normen/@BBN@/Geldig ? Normering/BBN/BouwdeelNorm/@BD@[Normering/Normen/@BBN@/BBN_rel] : (0/0)[Classifications/IsolatieNiveau]', '@BBN@', Normering/BBN/name))+', ApplyIsolatie/@BD@)', '@BD@', Classifications/Bouwdeel/name),
				UniekAmbitie,
				classifications/IsolatieNiveau);

			container IsChangedNow := for_each_nedv(
				classifications/Bouwdeel/name,
				replace('Isolatie/@BD > BO/Bouwdelen/Isolatie/@BD[BO_rel]'
				,	'@BD', classifications/Bouwdeel/name),
				UniekAmbitie, bool);

			attribute<bool> GeenAmbitie(UniekAmbitie) := ='and('+AsItemList(replace('not(IsChangedNow/@BD)','@BD', classifications/Bouwdeel/name))+')';
				
			container IsolatieMaatregel_rel := for_each_nedv(
				classifications/Bouwdeel/name,
				replace(
					'not(IsChangedNow/@BD) '
						'? (0/0)[Classifications/IsolatieMaatregel] '
						': classifications/BouwdeelIsolatie/IsolatieMaatregel_rel[combine_data(classifications/BouwdeelIsolatie, classifications/Bouwdeel/V/@BD , Isolatie/@BD)]'
				,	'@BD', classifications/Bouwdeel/name),
				UniekAmbitie, Classifications/IsolatieMaatregel);
							
			container Ki_gv := for_each_nedv(
				classifications/Bouwdeel/name,
				replace(
					'makedefined(BO/Bouwdelen/@BD[BO_rel] '
					'*  Classifications/IsolatieMaatregel/xUitvoering/kosten['
							'combine_data(Classifications/IsolatieMaatregel/xUitvoering'
							', IsolatieMaatregel_rel/@BD'
							', BO/Activatie/Uitvoering[BO_Rel]'
							')'
						']'
					', 0.0[Eur])'
				,	'@BD', classifications/Bouwdeel/name),
				UniekAmbitie, Eur)
			{
				attribute<Eur> Totaal(UniekAmbitie) := ='add('+AsItemList(Classifications/bouwdeel/name)+')';
			}
			container Kji_gv := for_each_nedv(
				  classifications/Bouwdeel/name
				, replace('Ki_gv/@BD * NCWrefs/AF_'+ string(Classifications/Bouwdeel/AT[Classifications/Bouwdeel]) 
						+'[BO_rel]', '@BD', classifications/Bouwdeel/name)
				, UniekAmbitie
				, Eur_yr)
			{
				attribute<Eur_yr> Totaal(UniekAmbitie) := ='add('+AsItemList(Classifications/bouwdeel/name)+')';
			}
			container Kmi_gv := for_each_nedv(
				  classifications/Bouwdeel/name
				, replace('Ki_gv/@BD * NCW/mr'+ string(Classifications/Bouwdeel/AT[Classifications/Bouwdeel]) 
						+'/AnnualisationFactor', '@BD', classifications/Bouwdeel/name)
				, UniekAmbitie
				, Eur_yr)
			{
				attribute<Eur_yr> Totaal(UniekAmbitie) := ='add('+AsItemList(Classifications/bouwdeel/name)+')';
			}
		}
		attribute<Eur>    Ki_gv  (UniekAmbitie):= Bouwdelen/Ki_gv/Totaal;
		attribute<Eur_yr> Kji_gv (UniekAmbitie):= Bouwdelen/Kji_gv/Totaal;
		attribute<Eur_yr> Kmi_gv (UniekAmbitie):= Bouwdelen/Kmi_gv/Totaal;
		
		// hier te berekenen: Funcionele vraag en Aansluitcapaciteit
		container Functioneel := CalculationSchemes/BerekenFunctioneel(UniekAmbitie, BO, BO_rel);

		attribute<Classifications/Schillabel> Schillabel_rel (UniekAmbitie) := Functioneel/Schillabel_rel; 
		
		container Aansluitcapaciteit //TODO: later invullen (nu dummygetal)
		{
			attribute<kW> RV (UniekAmbitie) := functioneel/RV * 0.25[kW_GJ_yr];
			attribute<kW> KD (UniekAmbitie) := BO/AansluitCapaciteit/KD[BO_rel];
			attribute<kW> TW (UniekAmbitie) := BO/AansluitCapaciteit/TW[BO_rel];
		}
	}
	
	template KiesBouwdeelT : ishidden = "True"
	{
		parameter<classifications/Bouwdeel> bouwdeel_rel;
		attribute<classifications/IsolatieNiveau> OrgIsolatieNiveau_rel(BouwdeelActieveWoning);
		
		container CumulP_i := for_each_nedv(classifications/IsolatieDoel/name
		, 'P/'+classifications/bouwdeel/name[bouwdeel_rel]+'_'+classifications/IsolatieDoel/name
		+ MakeDefined('+ '+classifications/IsolatieDoel/name[sub_or_null(ID(classifications/IsolatieDoel), 1b)], '')
		, AmbitieBerekening, float64
		);
		attribute<float64> rnd (BouwdeelActieveWoning) := rnd_uniform(JaarSeed * #Classifications/Bouwdeel + uint32(bouwdeel_rel), BouwdeelActieveWoning, range(float64, 0.0, 1.0));
		
		attribute<bool> Activatie (BouwdeelActieveWoning) := ='BO/Activatie/Bouwdelen/'+classifications/Bouwdeel/name[bouwdeel_rel]+'[BO_rel] || DynamicProgramForMinimumIsolatieNiveauEis';

		container CumulP := for_each_nedv(classifications/IsolatieDoel/name
		, 'CumulP_i/'+classifications/IsolatieDoel/name+' + (1d - CumulP_i/n4)'
		, AmbitieBerekening, float64
		)
		{
			attribute<float64> n0 (AmbitieBerekening) := 1d-CumulP_i/n4;
		}

		attribute<classifications/IsolatieNiveau> NewIsolatieNiveau_rel(AmbitieBerekening) := 
			not(Activatie[BouwdeelActieveWoning_rel]) ? OrgIsolatieNiveau_rel[BouwdeelActieveWoning_rel]		
		:	rnd[BouwdeelActieveWoning_rel] < CumulP/n0 ? max_elem(classifications/IsolatieNiveau/V/n0, OrgIsolatieNiveau_rel[BouwdeelActieveWoning_rel]) 	
		:	rnd[BouwdeelActieveWoning_rel] < CumulP/n1 ? max_elem(classifications/IsolatieNiveau/V/n1, OrgIsolatieNiveau_rel[BouwdeelActieveWoning_rel]) 	
		:	rnd[BouwdeelActieveWoning_rel] < CumulP/n2 ? max_elem(classifications/IsolatieNiveau/V/n2, OrgIsolatieNiveau_rel[BouwdeelActieveWoning_rel]) 	
		:	rnd[BouwdeelActieveWoning_rel] < CumulP/n3 ? max_elem(classifications/IsolatieNiveau/V/n3, OrgIsolatieNiveau_rel[BouwdeelActieveWoning_rel]) 	
		:	rnd[BouwdeelActieveWoning_rel] < CumulP/n4 ? max_elem(classifications/IsolatieNiveau/V/n4, OrgIsolatieNiveau_rel[BouwdeelActieveWoning_rel]) 	
		:	OrgIsolatieNiveau_rel[BouwdeelActieveWoning_rel];
	}
	attribute<bool> ProductActief := BO/Activatie/ProductActief[BouwdeelActieveWoning/BO_rel], ishidden = "True";
	
	unit<uint32> ProductInactieveBO := select_with_org_rel(not(ProductActief))
	{
		attribute<BouwdeelActieveWoning> BouwdeelActieveWoning_rel := org_rel;
		attribute<BO> BO_rel := BouwdeelActieveWoning/BO_rel[BouwdeelActieveWoning_rel];
	}
	
	unit<uint32> ProductInactieveAmbitie := select(IsDefined(rlookup(UniekAmbitie/BouwdeelActieveWoning_rel,ProductInactieveBO/BouwdeelActieveWoning_rel)))
	{
		attribute<ProductInactieveBO>                  ProductInactieveBO_rel  := collect_by_cond(., rlookup(UniekAmbitie/BouwdeelActieveWoning_rel,ProductInactieveBO/BouwdeelActieveWoning_rel)), ishidden = "True";
		
		attribute<classifications/IsolatieAmbitie>     MinIsolatieAmbitie_rel  := collect_by_cond(., UniekAmbitie/MinIsolatieAmbitie_rel), ishidden = "True";
		attribute<classifications/IsolatieAmbitie>     MaxIsolatieAmbitie_rel  := collect_by_cond(., UniekAmbitie/MaxIsolatieAmbitie_rel), ishidden = "True";
		
		attribute<bool>                                GeenAmbitie             := collect_by_cond(., UniekAmbitie/Bouwdelen/GeenAmbitie), ishidden = "True";
		
		attribute<BouwdeelActieveWoning> BouwdeelActieveWoning_rel := ProductInactieveBO/BouwdeelActieveWoning_rel[ProductInactieveBO_rel], ishidden = "True";
		attribute<BO> BO_rel := ProductInactieveBO/BO_rel[ProductInactieveBO_rel], ishidden = "True";
		
		attribute<Classifications/Schillabel> Schillabel_rel := GeenAmbitie ? BO/Schillabel_rel[BO_rel] : collect_by_cond(., UniekAmbitie/Schillabel_rel);
		
		container bouwdelen : descr = "uitkomsten per bouwdeel per ambitieniveau"
		{
			container isolatie := for_each_nedv(
				Classifications/Bouwdeel/name,
				replace('GeenAmbitie ? BO/Bouwdelen/isolatie/@BD@[BO_rel] : collect_by_cond(ProductInactieveAmbitie, UniekAmbitie/Bouwdelen/Isolatie/@BD@)', '@BD@', Classifications/Bouwdeel/name ),
				ProductInactieveAmbitie,
				Classifications/IsolatieNiveau);
		}
		container kosten := for_each_nedv(
				Classifications/Bouwdeel/name,
				replace('GeenAmbitie ? 0[EUR] : collect_by_cond(ProductInactieveAmbitie, UniekAmbitie/Bouwdelen/Ki_gv/@BD@)', '@BD@', Classifications/Bouwdeel/name ),
				ProductInactieveAmbitie,
				Eur);
		attribute<eur>    Ki_gv  (ProductInactieveAmbitie) := GeenAmbitie ? 0[EUR]    : collect_by_cond(., UniekAmbitie/Ki_gv );
		attribute<eur_yr> Kji_gv (ProductInactieveAmbitie) := GeenAmbitie ? 0[EUR_yr] : collect_by_cond(., UniekAmbitie/Kji_gv);
		attribute<eur_yr> Kmi_gv (ProductInactieveAmbitie) := GeenAmbitie ? 0[EUR_yr] : collect_by_cond(., UniekAmbitie/Kmi_gv);
		
		container functioneel := for_each_nedv(
				Classifications/FunctioneleVraag/name,
				replace('GeenAmbitie ? BO/functioneel/@PT@[BO_rel] : collect_by_cond(ProductInactieveAmbitie, UniekAmbitie/functioneel/@PT@)', '@PT@', Classifications/FunctioneleVraag/name ),
				ProductInactieveAmbitie, GJ_yr)
		{
			attribute<GJ_yr> Verlies_RV    (ProductInactieveAmbitie) := GeenAmbitie ? BO/functioneel/RV_per_Bouwdeel_now/Totaal[BO_rel] + BO/functioneel/Vrv_VT[BO_rel]: collect_by_cond(ProductInactieveAmbitie, UniekAmbitie/functioneel/RV_per_Bouwdeel_now/Totaal + UniekAmbitie/functioneel/Vrv_VT);
			attribute<GJ_yr> Winst_RV      (ProductInactieveAmbitie) := GeenAmbitie ? BO/functioneel/Winst_RV[BO_rel] : collect_by_cond(ProductInactieveAmbitie, UniekAmbitie/functioneel/Winst_RV);
			attribute<ratio> Correction_RV (ProductInactieveAmbitie) := GeenAmbitie ? BO/functioneel/Correction_RV[BO_rel]: collect_by_cond(ProductInactieveAmbitie, UniekAmbitie/functioneel/Correction_RV);
		}
		container AansluitCapaciteit := for_each_nedv(
				Classifications/ProductType/name,
				replace('GeenAmbitie ? BO/AansluitCapaciteit/@PT@[BO_rel] :collect_by_cond(ProductInactieveAmbitie, UniekAmbitie/AansluitCapaciteit/@PT@)', '@PT@', Classifications/ProductType/name ),
				ProductInactieveAmbitie, kW);	
	}
}