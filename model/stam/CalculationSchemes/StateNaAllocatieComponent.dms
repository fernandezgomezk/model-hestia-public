//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) Hestia 2021 - PBL & TNO                                        //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template StateNaAllocatieComponent
{
	// begin case parameters
	unit<uint32> BO_base;
	// end case parameters

	unit<uint32> BO := BO_base
	{
		container Metervraag   := BO_base/Metervraag;
		container BC_kentallen := BO_base/BC_kentallen;

		container Aansluitingen := BO_base/Aansluitingen
		{
			attribute<nrAsl> Gnet   (BO) := nrAansluitingen * float64(Metervraag/gas > 0[GJ_yr]);
			attribute<nrAsl> Warmte (BO) := = 'nrAansluitingen * float64(or('+AsItemList(replace('BO/InstallatiePerProduct/@PD@ == Classifications/Installatie/V/Gebiedsoptie','@PD@',Classifications/product/name))+'))';

			attribute<nrAsl> W70    (BO) := Warmte;
			attribute<nrAsl> W50    (BO) := const(0.0[nrAsl],BO);
			attribute<nrAsl> W30    (BO) := const(0.0[nrAsl],BO);
			attribute<nrAsl> WK70   (BO) := const(0.0[nrAsl],BO);
			attribute<nrAsl> WK50   (BO) := const(0.0[nrAsl],BO);
			attribute<nrAsl> WK30   (BO) := const(0.0[nrAsl],BO);
		}

		container eenmalig := for_each_nedv(
			Classifications/GebouwOptie_eenmalig/name,
			'BO_base/eenmalig/'+ Classifications/GebouwOptie_eenmalig/name,
			BO,
			Eur)
		{
			//Aansluitbijdrages
			attribute<Eur> Ki_Asl_Enet (BO) := BC_kentallen/Ki_Aansl_e_asl * nraansluitingen + BC_kentallen/Ki_Aansl_e_opp * oppervlakte;
			attribute<Eur> Ki_Asl_Gnet (BO) := Metervraag/gas <= 0[GJ_yr]
				? 0[Eur]
				: BC_kentallen/Ki_Aansl_g_asl * nraansluitingen + BC_kentallen/Ki_Aansl_g_opp * oppervlakte;
		}
		container Kapitaallasten := for_each_nedv(
			Classifications/Gebouw_Kapitaallasten/name, 
			'BO_base/Kapitaallasten/'+ Classifications/Gebouw_Kapitaallasten/name,
			BO,
			Eur_yr);

		container Jaarlijks := for_each_nedv(
			Classifications/GebouwOptie_jaarlijks/name,
			'BO_base/jaarlijks/'+ Classifications/GebouwOptie_jaarlijks/name,
			BO,
			Eur_yr)
		{
			attribute<eur_yr> oh_KD      (BO) := BO_base/jaarlijks/oh_KD;
			attribute<eur_yr> adm_KD     (BO) := BO_base/jaarlijks/adm_KD;
			
			container Eindgebruikerskosten
			{	
				attribute<Classifications/Eigendom>	Eigendom_rel	(BO) := BO/Eigendom_rel;				
				attribute<Eur> Ki_installatie 	(BO) := eenmalig/Ki15_LO + eenmalig/Ki_VT + eenmalig/Ki20_LO + eenmalig/Ki30_LO + eenmalig/Ki_LTAS - eenmalig/Oi15_LO - eenmalig/Oi_VT - eenmalig/Oi20_LO - eenmalig/Oi30_LO; //check of LTAS hier ook bijhoort
				attribute<Eur> Ki_isolatie 		(BO) := eenmalig/Ki_gv - eenmalig/Oi_gv;
					
				container Financierlingslasten  		// Hypothecaire lening voor eigenaar-bewoner, vastgoedhypotheek voor particuliere verhuurder, WSW voor sociale verhuurder
				{
					attribute<percent_yr> rentepercentage 	(BO) := ='merge(uint8(BO/Eigendom_rel), percent_yr,'+ AsItemList('Invoer/Kengetallen/Eindgebruikerskosten/rentepercentages/PerJaar/R_' + Classifications/Eigendom/c + '[Classifications/Zichtjaar/Rekenjaar_rel[Zichtjaar_rel]]')+')';
					attribute<_yr> maandrente				(BO) := (rentepercentage[percent_yr] / 10000d)[_yr] / 12d;
					attribute<Eur_yr> Kj_installatie 		(BO) := 12.0 * ( maandrente / (1.0-((1.0 + maandrente)^-180.0)) ) * Ki_installatie;
					attribute<Eur_yr> Kj_isolatie			(BO) := Eigendom_rel == 0 ? 12.0 * ( maandrente / (1.0-((1.0 + maandrente)^-360.0)) ) * Ki_isolatie : 12.0 * ( maandrente / (1.0-((1.0 + maandrente)^-300.0)) ) * Ki_isolatie;					
					attribute<Eur_yr> Kj_totaal				(BO) := Kj_installatie + Kj_isolatie;
					
					container Warmtefondslening		// Warmtefondslening voor eigenaar-bewoner, null voor huurwoningen
					{
						attribute<percent_yr> rentepercentage	(BO) := Eigendom_rel == 0 ? const(Invoer/Kengetallen/Eindgebruikerskosten/rentepercentages/PerJaar/R_wf[Classifications/Zichtjaar/Rekenjaar_rel[Zichtjaar_rel]],BO) : float64(0/0);
						attribute<_yr> maandrente				(BO) := (rentepercentage[percent_yr] / 10000d)[_yr] / 12d;
						attribute<Eur_yr> Kj_installatie		(BO) := 12.0 * ( maandrente / (1.0-((1.0 + maandrente)^-180.0)) ) * Ki_installatie;
						attribute<Eur_yr> Kj_isolatie			(BO) := 12.0 * ( maandrente / (1.0-((1.0 + maandrente)^-240.0)) ) * Ki_isolatie; 
						attribute<Eur_yr> Kj_totaal				(BO) := Kj_installatie + Kj_isolatie;
					}
				}
				
				container Huurverhoging_huurcommissiemethode 
				{
					attribute<percent_yr> rentepercentage	(BO) := Eigendom_rel != 0 ? const(Invoer/Kengetallen/Eindgebruikerskosten/rentepercentages/PerJaar/R_hc[Classifications/Zichtjaar/Rekenjaar_rel[Zichtjaar_rel]],BO) : float64(0/0);
					attribute<_yr> maandrente				(BO) := rentepercentage[_yr] / 10000d / 12d;
					attribute<Eur_yr> Kj_installatie 		(BO) := 12.0 * ( maandrente / (1.0-((1.0 + maandrente)^-180.0)) ) * Ki_installatie;
					attribute<Eur_yr> Kj_isolatie			(BO) :=	Eigendom_rel == 1 ? 12.0 * ( maandrente / (1.0-((1.0 + maandrente)^-300.0)) ) * Ki_isolatie : 0d;
					attribute<Eur_yr> Huurverhoging         (BO) := Kj_installatie + Kj_isolatie;
				}

				//====== Placeholder Hypotheekrenteaftrek
				container Hypotheekrenteaftrek
				{
					parameter<ratio> BelastingSchijf := 0.37[ratio]; // 0.371 of 0.495 (2022)

					attribute<Eur_yr> KL_totaal    (BO) := = 'add('+AsItemlist('float64('+string(Classifications/Gebouw_Kapitaallasten/isEindgebruikerskosten)+ ') * Kapitaallasten/'+ Classifications/Gebouw_Kapitaallasten/name)+')';
					attribute<Eur_yr> wv_Aflossing (BO) := = 'add('+AsItemlist('eenmalig/'+ Classifications/Gebouwoptie_eenmalig/name + ' / float64(' + string(Classifications/GebouwOptie_eenmalig/AT) +')[yr]' ) +')' ;
					attribute<Eur_yr> RenteBedrag  (BO) := KL_totaal - wv_Aflossing;
					attribute<Eur_yr> Aftrek       (BO) := RenteBedrag * BelastingSchijf;
				}
			}
		}
	}
}

